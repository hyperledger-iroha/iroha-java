plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlinVer"
    id "org.jlleitschuh.gradle.ktlint" version "$kotlinLinterVer"
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        flatDir {
            dirs "${project.rootDir}/libs"
        }
    }
}

subprojects {
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: 'maven'

    group = 'jp.co.soramitsu.iroha-java'
    version = '0.0.1-SNAPSHOT'

    dependencies {
        api fileTree(dir: 'libs', include: ['*.jar'])
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.5'
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:$kotlinVer"
        testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    }

    test {
        useJUnitPlatform()
    }

    jar {
        from {
            configurations.runtimeClasspath
            //todo remove it as soon as will be own implementation of scale codec
            .filter { it.name.contains("polkaj-scale") }
            .collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
    }
    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = 11
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = 11
        }
    }
}
